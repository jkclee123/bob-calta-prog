Name: Date Duration 數日子
Mode: REG Lin 
Memory: 294

Program:
?→X:?→Y:ClrStat:Deg:X,Ydt:0→M:Fix 0: /24
Rnd(G-.5+X₁₀^(-4→A:X-A₁₀^(4→X: /45
Rnd(G-.5+Y₁₀^(-4→B:Y-B₁₀^(4→Y: /66
0→C:X≥301⇒1→C: /80
For A→A To B-1:A+C: /93
M+365+(sin(45Ans)=0)(sin(1.8Ans)≠0)+(0=sin(.45Ans→M: /130
Next: /132
Rnd(.01X-.5→C:Rnd(.01Y-.5→D:M-X+Y+100(C-D→M: /170
∑x→X:∑y→Y:1→A:If C>D:Then C→A:D→C:A→D:-1→A:IfEnd: /207
For C→C To D-1: /216
If C≠2:Then M+A(31-(1=Abs(Abs(C-7.5)-2.5→M: /247
Else M+A(28+(sin(45B)=0)(sin(1.8B)≠0)+(0=sin(.45B→M:IfEnd: /288
Next:Norm 2:M /294

Guide:
ClrStat: in CLR menu (shift 9)
dt: M⁺
Rnd: shift 0
G: in CONST menu (shift 7)
°: shift Ans
∑x: in S-VAR menu (shift 2)
∑y: in S-VAR menu (shift 2)

Usage:
X?: start date [yyyyMMdd]
Y?: end date [yyyyMMdd]

Example:
X: 20240415
Y: 20250609
M: 420 (excluding end date)

Leap Year:
    1. Program Statement
        ((sin(45Y)=0)(sin(1.8Y)≠0))+(0=sin(.45Y

    2. Intuitive rule
        1. A year is a leap year if it’s divisible by 4. 
           年係閏年如果除得盡4
        2. Except if it’s also divisible by 100, in which case it’s not a leap year. 
           除非除得盡100
        3. Unless it’s also divisible by 400—in which case it is a leap year after all. 
           除非除非除得盡400

    3. Symbolic version
        Using “a∣b” to mean “a divides b", and “a∤b” for “a does not divide b”:
        leap(y) ⟺ (4|y ∧ 100∤y) ∨ (400∣y)
        > 4∣y forces a multiple of 4. 
          4∣y 除得盡4
        > 100∤y rules out century years (e.g. 1700). 
          100∤y 除唔盡100
        > 400∣y re-includes the “every 400th” century (e.g. 2000). 
          400∣y 除得盡400

    4. Trigonometric shortcut 
        On a calculator, you can encode “divisible by n” as:
            sin(180°y/n) = 0 ⟺ n|y
        In particular:
            sin(45°y) = 0 ⟺ 4|y
            sin(1.8°y) ≠ 0 ⟺ 100∤y
            sin(0.45°y) = 0 ⟺ 400|y
        Leap Year Formula:
            leap(y) ⟺ ((sin(45y°) = 0) * (sin(1.8y°) ≠ 0)) + (sin(0.45y°) = 0)
            leap(y) = 1 if leap year (e.g. 2000, 2024)
            leap(y) = 0 if not leap year (e.g. 1700, 2025)



Months with 30 days:
    program statement: (1=Abs(Abs(C-7.5)-2.5
    > April, June, September and November are months with 30 days, this statement is used to filter them
    > 一年得4月、6月、9月同11月有30日，公式可以分辨呢4個月
    f(x) = ||x - 7.5| - 2.5| = 1
    f(x) = 1 if x is 4, 6, 9, 11 in range (1 - 12)
    f(x) = 0 else
    





